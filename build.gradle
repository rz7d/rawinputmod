plugins {
    id "java-library"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "net.minecraftforge.gradle" version "[6.0,6.2)"
}

version = "2.0.1"
group = "com.github.asteriskmods.inputtweaks"

base {
    archivesName = "InputTweaksX"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
    mappings channel: "snapshot", version: "20180814-1.12"
    copyIdeResources = true

    runs {
        configureEach {
            workingDirectory project.file("run")
            property "forge.logging.markers", "REGISTRIES"
            property "forge.logging.console.level", "debug"
            mods {
                "inputtweaks" {
                    source sourceSets.main
                }
            }
        }
        client {
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"
    compileOnly "org.projectlombok:lombok:1.18.28"
    annotationProcessor "org.projectlombok:lombok:1.18.28"
    testCompileOnly "org.projectlombok:lombok:1.18.28"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.28"
}

processResources {
    inputs.property "name", name
    inputs.property "version", project.version
    inputs.property "mcversion", "1.12.2"

    filesMatching("mcmod.info") {
        include "mcmod.info"
        expand "version": project.version, "mcversion": "1.12.2"
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

jar {
    manifest {
        attributes([
                'Specification-Title'     : "InputTweaks",
                'Specification-Vendor'    : "rz7d",
                'Specification-Version'   : "1",
                'Implementation-Title'    : project.name,
                'Implementation-Version'  : project.jar.archiveVersion,
                'Implementation-Vendor'   : "rz7d",
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    finalizedBy "reobfJar"
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

//sourceSets {
//    main {
//        output.resourcesDir = output.classesDirs[0]
//    }
//}
